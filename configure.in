########################################################################
#
# File:   configure.in
# Author: Alex Samuel
# Date:   2001-01-09
#
# Contents:
#   Autoconf configuration for QM.
#
# Copyright (c) 2001 by CodeSourcery, LLC.  All rights reserved. 
#
# Permission is hereby granted, free of charge, to any person
# obtaining a copy of this software and associated documentation files
# (the "Software"), to deal in the Software without restriction,
# including without limitation the rights to use, copy, modify, merge,
# publish, distribute, sublicense, and/or sell copies of the Software,
# and to permit persons to whom the Software is furnished to do so,
# subject to the following conditions:
#
# The above copyright notice and this permission notice shall be
# included in all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
# EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
# MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
# NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS
# BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN
# ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN
# CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.
#
########################################################################

AC_INIT(configure.in)

dnl ####################################################################
dnl Enable maintainer mode.
dnl ####################################################################

AC_ARG_ENABLE(maintainer-mode, 
              [  --enable-maintainer-mode configure for development],
              if test x${enableval} = xno; then
                MAINTAINER_MODE=0
              else
                MAINTAINER_MODE=1
              fi,
              MAINTAINER_MODE=0)
AC_SUBST(MAINTAINER_MODE)
	     
dnl ####################################################################
dnl Set the version numbers for QM.  These values should be set here 
dnl and nowhere else in QM.
dnl ####################################################################

QM_MAJOR_VER=1
QM_MINOR_VER=0
QM_RELEASE_VER=0

AC_SUBST(QM_MAJOR_VER)
AC_SUBST(QM_MINOR_VER)
AC_SUBST(QM_RELEASE_VER)

# For a dot release, the release is named "X.Y.Z"; for other releases
# the name is just "X.Y".
if test ${QM_RELEASE_VER} -ne 0; then
    QM_VERSION=${QM_MAJOR_VER}.${QM_MINOR_VER}.${QM_RELEASE_VER}
else
    QM_VERSION=${QM_MAJOR_VER}.${QM_MINOR_VER}
fi

AC_SUBST(QM_VERSION)

dnl ####################################################################
dnl Find some common programs.
dnl ####################################################################

AC_PROG_MAKE_SET

dnl ####################################################################
dnl Find the Python interpreter.  If the user specified the 
dnl --with-python option, use its value, after checking that it exists.
dnl Otherwise, look in the PATH.
dnl ####################################################################

AC_ARG_WITH(
  python, 
  [  --with-python=PATH      specify the Python interpreter],
  PYTHON="$with_python",
  PYTHON=""
)

if test -n "$PYTHON" -a "$PYTHON" != yes; then
  AC_CHECK_FILE(
    $PYTHON, 
    , 
    echo "Cannot find Python interpreter $PYTHON."; exit 1
  )
else
  AC_PATH_PROG(PYTHON, python, python)
fi

dnl ####################################################################
dnl Determine the Python version, installation prefix, and and platform.
dnl ####################################################################

AC_MSG_CHECKING(Python version)
PYTHON_VERSION=`$PYTHON -c "import sys; import string; print string.split(sys.version, ' ')[[0]][[:3]]"`
AC_MSG_RESULT($PYTHON_VERSION)
AC_SUBST(PYTHON_VERSION)

AC_MSG_CHECKING(Python prefix)
PYTHON_PREFIX=`$PYTHON -c "import sys; print sys.prefix"`
AC_MSG_RESULT($PYTHON_PREFIX)
AC_SUBST(PYTHON_PREFIX)

AC_MSG_CHECKING(Python platform)
PYTHON_PLATFORM=`$PYTHON -c "import distutils.util; print distutils.util.get_platform()"`
AC_MSG_RESULT($PYTHON_PLATFORM)
AC_SUBST(PYTHON_PLATFORM)

dnl ####################################################################
dnl Look for HappyDoc, which we use to build source documentation.
dnl ####################################################################

AC_PATH_PROG(HAPPYDOC, happydoc, no)

dnl ####################################################################
dnl Jade and Tidy are used to generate documentation.  Without Jade,
dnl documentation will not be built.
dnl ####################################################################

AC_PATH_PROG(JADE, jade, no)
AC_PATH_PROG(TIDY, tidy, no)

dnl ####################################################################
dnl We need XML declarations for Jade, in xml.dcl.  This file should be 
dnl included with Jade.  Look for it in some usual places.
dnl ####################################################################

if test "$JADE" != no; then
  AC_MSG_CHECKING(xml.dcl)
  XML_DCL=no
  for file in \
    /usr/share/doc/jade*/pubtext/xml.dcl \
    /usr/share/doc/openjade*/pubtext/xml.dcl \
    /usr/doc/jade*/pubtext/xml.dcl \
  ; do
    if test -r "$file"; then
      XML_DCL=$file
      break
    fi
  done
  AC_MSG_RESULT($XML_DCL)
  AC_SUBST(XML_DCL)
fi

dnl ####################################################################
dnl To build documentation, we need DSSSL stylesheets for DocBook.  Look
dnl for them in some usual places.
dnl ####################################################################

AC_MSG_CHECKING(DocBook stylesheets)
DOCBOOK_STYLESHEETS=no
for dir in \
  /usr/lib/sgml/stylesheets/docbook \
; do
  if test -d $dir; then
    DOCBOOK_STYLESHEETS=$dir
    break
  fi
done
AC_MSG_RESULT($DOCBOOK_STYLESHEETS)
AC_SUBST(DOCBOOK_STYLESHEETS)

dnl #####################
dnl Find the Docbook DTD.
dnl #####################

DOCBOOK_DTD=
AC_MSG_CHECKING(for dobook DTD)
AC_ARG_WITH(docbook-dtd, 
            [  --with-docbook-dtd=PATH directory containing the docbook DTD.],
            [case $withval in
               yes) DOCBOOK_DTD=/usr/lib/sgml;;
               no)  ;;
               *)   DOCBOOK_DTD=$withval;;
             esac],
            DOCBOOK_DTD=/usr/lib/sgml)
AC_MSG_RESULT($DOCBOOK_DTD)
AC_SUBST(DOCBOOK_DTD)

if test "$JADE" = no -o "$XML_DCL" = no -o "$DOCBOOK_DTD" = no; then
  AC_MSG_WARN(Documentation will not be built.)
  DOCUMENTATION=no
else
  DOCUMENTATION=yes
fi

# Do not try to build documentation unless --enable-maintainer-mode
# was specified.  The documentation is pre-generated for releases.

if test $DOCUMENTATION = yes && test $MAINTAINER_MODE = 0; then
  AC_MSG_WARN(Documentation is only built in maintainer mode.)
  DOCUMENTATION=no
fi

AC_SUBST(DOCUMENTATION)

dnl ####################################################################
dnl Generate results
dnl ####################################################################

AC_OUTPUT(
  standard.mk
  Makefile
  qm.spec
  doc/Makefile
  doc/design/qmtest/Makefile
  doc/manual/Makefile
  qm/setup_path.py
)

dnl ####################################################################
dnl This follows the setup instructions in the Gadfly package.  The
dnl target is a file generated in the setup procedure.  See
dnl gadfly/index.html.
dnl ####################################################################

(cd gadfly; $PYTHON gfinstall.py)
